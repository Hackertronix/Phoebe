apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'realm-android'

ext {
    SUPPORT_LIB = "25.0.0"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "liam.franco.selene"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode getVersionCodeTimestamp()
        versionName "1.0b"
        archivesBaseName = "Selene-${android.defaultConfig.versionName}"
    }

    signingConfigs {
        release {
            readKeystoreProperties('release')
        }

        debug {
            readKeystoreProperties('debug')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [vmSafeMode: "false"]
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            shrinkResources false
            manifestPlaceholders = [vmSafeMode: "true"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def readKeystoreProperties(def configName) {
    // edit this path to your keystore file
    def propsFile = rootProject.file('../../../keystore.properties')

    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        android.signingConfigs[configName].storeFile = file(props['storeFile'])
        android.signingConfigs[configName].storePassword = props['storePassword']
        android.signingConfigs[configName].keyAlias = props['keyAlias']
        android.signingConfigs[configName].keyPassword = props['keyPassword']
    }
}

def getVersionCodeTimestamp() {
    return new Date().format('yyMMddHHmm').toInteger()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${SUPPORT_LIB}"
    compile "com.android.support:cardview-v7:${SUPPORT_LIB}"
    compile "com.android.support:design:${SUPPORT_LIB}"
    compile "com.android.support:customtabs:${SUPPORT_LIB}"
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'de.halfbit:tinybus:3.0.2'
    compile 'frankiesardo:icepick:3.2.0'
    compile('com.mikepenz:materialize:1.0.0@aar') {
        transitive = true
    }
    compile('com.mikepenz:fastadapter:2.0.0@aar') {
        transitive = true
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
